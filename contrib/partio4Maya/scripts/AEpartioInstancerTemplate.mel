/* partio4Maya  3/12/2012, John Cassella  http://luma-pictures.com and  http://redpawfx.com
PARTIO Export
Copyright 2012 (c)  All rights reserved

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are
met:

* Redistributions of source code must retain the above copyright
notice, this list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in
the documentation and/or other materials provided with the
distribution.

Disclaimer: THIS SOFTWARE IS PROVIDED BY  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING,
BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE, NONINFRINGEMENT AND TITLE ARE DISCLAIMED.
IN NO EVENT SHALL  THE COPYRIGHT HOLDER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND BASED ON ANY
THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
*/


global proc AEpartioInstancerTemplate( string $nodeName )
{
    editorTemplate -callCustom pioInstLogoHeader pioInstLogoHeader $nodeName;
    editorTemplate -beginLayout "Global" -collapse 0;
    editorTemplate -addControl "cacheActive";
    editorTemplate -addControl "staticCache";
    //editorTemplate -addControl "flipYZ";
    editorTemplate -addControl "computeVeloPos";
    editorTemplate -addControl "velocitySource";
    editorTemplate -addControl -l "Velocity Mult" "veloMult";
    editorTemplate -callCustom pioAEoutpathNew pioAEoutpathReplace cacheDir;
    editorTemplate -addControl "cachePrefix";
    editorTemplate -addControl "cacheOffset";
    editorTemplate -addControl "byFrame";
    editorTemplate -addControl "cacheFormat";
    editorTemplate -addControl "iconSize";
    editorTemplate -callCustom pioInstForceUpdate pioInstForceUpdate $nodeName;

    editorTemplate -endLayout;

    editorTemplate -beginScrollLayout;

        editorTemplate -beginLayout "General Parameters" -collapse 0;
            editorTemplate -addControl "pointSize";
            editorTemplate -addControl "drawStyle";
            //editorTemplate -addControl "useEmitterTransform";
            //editorTemplate -addControl -l "Jitter Position Magnitude" "jitterPos" ;
            //editorTemplate -addControl -l "Jitter Position Frequency" "jitterFreq";
        editorTemplate -endLayout;
        editorTemplate -beginLayout "Instancer Channels" -collapse 0;
        editorTemplate -callCustom "pioInstCustomAttrsManage" "pioInstCustomAttrsManage" $nodeName;
        editorTemplate -endLayout;

    // include/call base class/node attributes
    //AEdependNodeTemplate $nodeName;
    editorTemplate -beginLayout ("Render Stats");
        editorTemplate -beginNoOptimize;
        editorTemplate -addControl "castsShadows";
        editorTemplate -addControl "receiveShadows";
        editorTemplate -addControl "motionBlur";
        editorTemplate -addControl "primaryVisibility";
        editorTemplate -addControl "smoothShading";
        editorTemplate -addControl "visibleInReflections";
        editorTemplate -addControl "visibleInRefractions";
    editorTemplate -endLayout;

    AEshapeTemplate $nodeName;  // adds place for renderer specific controls

    editorTemplate -suppress "compInstObjGroups";
    editorTemplate -suppress "ignoreSelfShadowing";
    editorTemplate -suppress "geometryAntialiasingOverride";
    editorTemplate -suppress "antialiasingLevel";
    editorTemplate -suppress "shadingSamplesOverride";
    editorTemplate -suppress "shadingSamples";
    editorTemplate -suppress "maxShadingSamples";
    editorTemplate -suppress "volumeSamplesOverride";
    editorTemplate -suppress "volumeSamples";
    editorTemplate -suppress "depthJitter";
    editorTemplate -suppress "maxVisibilitySamplesOverride";
    editorTemplate -suppress "maxVisibilitySamples";
    editorTemplate -suppress "localPosition";
    editorTemplate -suppress "localScale";


    editorTemplate -addExtraControls;
    editorTemplate -endScrollLayout;
}

proc generateAttributesMenu(string $nodeName, string $attrNameList[], string $labels[], string $attributes[])
{
    int $i = 0;
    for ($i = 0; $i < size($attributes); ++$i)
    {
        attrControlGrp -annotation $labels[$i] -attribute ($nodeName + "." + $attributes[$i]);
        popupMenu;
        for ($attr in $attrNameList)
        {
            menuItem -label $attr -c ("setAttr -type \"string\" " + $nodeName + "." + $attributes[$i] + " " + $attr);
        }
    }
}

global proc pioInstCustomAttrsManage (string $nodeName)
{
    $nodeName = pioStripNodeName ($nodeName);
    if (`columnLayout -q -exists "foobar"`)
    { deleteUI "foobar"; }

    columnLayout -rs 0 foobar;

    string $multiList[] = `listAttr -multi ($nodeName+".partioCacheAttributes")`;

    string $attrNameList[] = {};
    int $id = 0;
    for ($multi in $multiList)
    {
        $attrNameList[$id] = `getAttr ($nodeName + "." + $multi)`;
        $id = $id + 1;
    }

    ///  General attrs
    frameLayout -collapse 0 -collapsable 0 -borderVisible 0 -w 450 -l "General Channels" ;
    /// SCALE
        columnLayout;
        string $scaleAttributes[] = {"scaleFrom"};
        string $scaleLabels[] = {"Scale"};
        generateAttributesMenu($nodeName, $attrNameList, $scaleLabels, $scaleAttributes);
    setParent ..;
    setParent ..;
    /// ROTATION stuff
    frameLayout -collapse 0 -collapsable 0 -borderVisible 0 -w 450 -l  "Rotation Channels" ;
    columnLayout;
    /// ROT TYPE
    string $rotationAttributes[] = {"rotationType", "rotationFrom", "aimDirectionFrom", "aimPositionFrom", "aimAxisFrom", "aimUpAxisFrom", "aimWorldUpFrom"};
    string $rotationLabels[] = {"Rotation Type", "Rotation", "Aim Direction", "Aim Position", "Aim Axis", "Aim Up Axis", "Aim World Up"};
    generateAttributesMenu($nodeName, $attrNameList, $rotationLabels, $rotationAttributes);

    setParent ..;
    setParent ..;

    ///  INDEX
    frameLayout -collapse 0 -collapsable 0 -borderVisible 0 -w 450 -l "Index Channels" ;
    string $indexAttributes[] = {"indexFrom"};
    string $indexLabels[] = {"Index"};
    generateAttributesMenu($nodeName, $attrNameList, $indexLabels, $indexAttributes);
    setParent ..;
    setParent ..;

    /// LAST ATTRIBUTES
    frameLayout -collapse 1 -collapsable 1 -borderVisible 0 -w 450 -l "Motion blur Channels (if left blank they will inherit above channels)" ;
    columnLayout;
    string $lastAttributes[] = {"velocityFrom", "lastPositionFrom", "lastRotationFrom", "lastAimDirectionFrom", "lastAimPositionFrom"};
    string $lastLabels[] = {"Velocity", "Last Position", "Last Rotation", "Last Aim Direction", "Last Aim Position"};
    generateAttributesMenu($nodeName, $attrNameList, $lastLabels, $lastAttributes);
    setParent ..;
    setParent ..;

    frameLayout -collapse 1 -borderVisible 0 -w 450 -l "Custom Attributes Export";

    attrControlGrp -attribute ($nodeName+".exportAttributes") "ExportAttributesCtrlGrp";

    setParent ..;
    setParent ..;
}

global proc pioInstForceUpdate(string $nodeName)
{
    $nodeName = pioStripNodeName ($nodeName);
    if (!`button -q -exists  pioInstReloadButton`)
    {   rowLayout -nc 1 ;
        button -ebg true -bgc 1 0.678 0.125 -w 100 -l "---<Force Reload>---"  -c ("setAttr "+$nodeName+".forceReload  true") pioInstReloadButton;
    }
}

global proc  pioInstLogoHeader(string $nodeName)
{
    if (!`symbolButton -q -exists  pioInstHeaderIcon`)
    {   rowLayout -nc 2 ;
        symbolButton -h 45 -w 235 -c "pioInstSwapAboutText()" -image "partioInstancer_logo.png" pioInstHeaderIcon;
        text -fn "smallBoldLabelFont" -l "Partio Instancer!!" pioInstAboutText;
    }
}

global proc  pioInstSwapAboutText()
{
    string $text1 =  "Partio Instancer!!";
    string $text2 =  "Partio Instancer by: \n John Cassella  (redpawfx)\n  with Luma-Pictures  2012";

    string $text = `text -q -l pioInstAboutText`;
    if (`gmatch $text "Partio Instancer!!"`)
    {
        text -e -l $text2  pioInstAboutText;
    }
    else
    {
        text -e -l $text1 pioInstAboutText;
    }
}
